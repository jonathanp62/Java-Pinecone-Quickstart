/*
 * (#)build.gradle  0.4.0   06/04/2025
 * (#)build.gradle  0.3.0   05/26/2025
 * (#)build.gradle  0.2.0   05/19/2025
 * (#)build.gradle  0.1.0   05/17/2025
 *
 * @author   Jonathan Parker
 * @version  0.4.0
 * @since    0.1.0
 *
 * MIT License
 *
 * Copyright (c) 2025 Jonathan M. Parker
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

/*
 * The following system property must be set in HOME/.gradle/gradle.properties:
 *   systemProp.macosArchitecture=silicon
 */

plugins {
	id 'java'
	id 'net.jmp.gradle.plugin.projectinfo-info'	version '0.1.0'
}

apply plugin: 'net.jmp.gradle.plugin.projectinfo-info'

ext {
	embeddingModel = 'llama-text-embed-v2'
	embeddingModelSparse = 'pinecone-sparse-english-v0'
	indexName = 'quickstart'
	indexNameHybrid = 'quickstart-hybrid'
	javaMainClass = 'net.jmp.pinecone.quickstart.Main'
	mongoDbCollection = 'quickstart'
	mongoDbName = 'pinecone_quickstart'
	mongoDbUri = 'etc/mongo-db-uri.txt'
	namespace = 'quickstart-namespace'
	openaiApiKey = 'etc/open-ai-api-key.txt'
	pineconeApiKey = 'etc/pinecone-api-key.txt'
	project.group = 'net.jmp.pinecone.quickstart'
	project.version = '0.4.0'
	project.description = 'to demonstrate the Pinecone Quickstart'
	rerankingModel = 'bge-reranker-v2-m3'
}

java.sourceCompatibility = JavaVersion.VERSION_24

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation		(libs.bundles.logging)
	implementation		(libs.com.openai.openai.java)
	implementation		(libs.io.pinecone.pinecone.client)
	implementation		(libs.org.mongodb.mongodb.driver.sync)

	runtimeOnly			(libs.bundles.logging.runtime)

	testRuntimeOnly		('org.junit.platform:junit-platform-launcher')
}

tasks.compileJava {
    options.forkOptions.jvmArgs = ["-Xmx8192m", "--enable-native-access=ALL-UNNAMED"]
}

tasks.withType(Javadoc).configureEach {
    CoreJavadocOptions cjo = options as CoreJavadocOptions
    StandardJavadocDocletOptions sjdo = options as StandardJavadocDocletOptions

    cjo.encoding("UTF-8")
    cjo.addStringOption("source", "24")
    cjo.memberLevel = JavadocMemberLevel.PRIVATE

    sjdo.version(true)
}

tasks.withType(JavaExec).configureEach {
	jvmArgs = ["-Xmx8192m",
			   "--enable-native-access=ALL-UNNAMED",
			   "--sun-misc-unsafe-memory-access=allow",	// Stop the JVM from complaining about Netty
			   "-ea"]

	println()
	println("JVM Arguments: " + jvmArgs)

	systemProperties(
			['app.embeddingModel': embeddingModel,
			 'app.embeddingModelSparse': embeddingModelSparse,
			 'app.indexName': indexName,
			 'app.indexNameHybrid': indexNameHybrid,
			 'app.mongoDbCollection': mongoDbCollection,
			 'app.mongoDbName': mongoDbName,
			 'app.mongoDbUri': mongoDbUri,
			 'app.namespace': namespace,
			 'app.openaiApiKey': openaiApiKey,
			 'app.pineconeApiKey': pineconeApiKey,
			 'app.rerankingModel': rerankingModel,
			 'app.queryText': '']
	)

	classpath = sourceSets.main.runtimeClasspath
	javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
	mainClass = javaMainClass
}

tasks.register('create', JavaExec) {
	getSystemProperties().put('app.operation', 'create')
}

tasks.register('delete', JavaExec) {
	getSystemProperties().put('app.operation', 'delete')
}

tasks.register('describe', JavaExec) {
	getSystemProperties().put('app.operation', 'describe')
}

tasks.register('describeModels', JavaExec) {
	getSystemProperties().put('app.operation', 'describeModels')
}

tasks.register('describeNamespace', JavaExec) {
	getSystemProperties().put('app.operation', 'describeNamespace')
}

tasks.register('fetch', JavaExec) {
	getSystemProperties().put('app.operation', 'fetch')
}

tasks.register('list', JavaExec) {
	getSystemProperties().put('app.operation', 'list')
}

tasks.register('listIndexes', JavaExec) {
	getSystemProperties().put('app.operation', 'listIndexes')
}

tasks.register('listNamespaces', JavaExec) {
	getSystemProperties().put('app.operation', 'listNamespaces')
}

tasks.register('load', JavaExec) {
	getSystemProperties().put('app.operation', 'load')
}

tasks.register('query-by-id', JavaExec) {
	var sysProps = getSystemProperties()

	sysProps.put('app.operation', 'query-by-id')
	sysProps.put('app.queryText', 'rec20')
}

tasks.register('query-monuments', JavaExec) {
	var sysProps = getSystemProperties()

	sysProps.put('app.operation', 'query-monuments')
	sysProps.put('app.queryText', 'Famous historical structures and monuments')
}

tasks.register('query-persons', JavaExec) {
	var sysProps = getSystemProperties()

	sysProps.put('app.operation', 'query-persons')
	sysProps.put('app.queryText', 'Tell me about famous persons in history and science')
}

tasks.register('store', JavaExec) {
	getSystemProperties().put('app.operation', 'store')
}
